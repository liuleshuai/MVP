apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.android["externalCompileSdkVersion"]
    defaultConfig {
        applicationId "com.liuleshuai.mvp"
        minSdkVersion rootProject.ext.android["externalMinSdkVersion"]
        targetSdkVersion rootProject.ext.android["externalTargetSdkVersion"]
        versionCode rootProject.ext.android["externalVersionCode"]
        versionName rootProject.ext.android["externalVersionName"]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary= true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }
    buildTypes {
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //混淆
            minifyEnabled false
            //Zipalign优化
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //apk命名
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def outputFile = output.outputFile
                    def releaseTime = new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
                    def fileName = "MVP-${defaultConfig.versionCode}-${defaultConfig.versionName}-${releaseTime}.apk"
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        //这里修改apk文件名
                        outputFileName = fileName
                    }
                }
            }
        }
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            minifyEnabled false
            zipAlignEnabled false
        }
    }
    //打包不检查
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.android.support:appcompat-v7:${rootProject.ext.android["androidSupportVersion"]}"
    testCompile 'junit:junit:4.12'
    compile project(':extralibrary:common')

    //butterKnife
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:latest.integration'

    //lottie
    compile('com.airbnb.android:lottie:2.5.1') {
        exclude group: 'com.android.support'
    }

    //路由
    annotationProcessor 'com.alibaba:arouter-compiler:latest.integration'

    //leakcanary(放在common里不起作用，代码打不进主app中)
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}
